# podria tenir falles
sp {hr2i*propose*grasp*obj1
   (state <s> ^name hr2i
              ^robot <r>
              ^object <obj>
              ^desired <d>)
   (<r> ^locId <rloc>
        ^obj1Id -1)
   (<obj> ^id <oid>
          ^locId <rloc>
         ^grasped no
         ^found yes
         ^delivered no)
   (<d> ^object <do>)
   (<do> ^id <oid>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name grasp
         ^objToGrasp <obj>
         ^withRob <r>)
}

sp {hr2i*apply*grasp
   (state <s> ^operator <op>
              ^io.output-link <out>)
   (<op> ^name grasp
         ^objToGrasp <obj>)
   (<obj> ^id <objid>)
-->
   (<out> ^grasp.obj <objid>)
}

sp {hr2i*apply*grasp*remove-grasp
   (state <s> ^operator <op>
              ^io.output-link <out>
              ^object <obj>
              ^robot <r>)
   (<op> ^name grasp
         ^objToGrasp <objop>)
   
   (<r> ^obj1Id <robjid>)
   
   (<objop> ^id <objid>)
   (<obj> ^id <objid>)
   
   (<out> ^grasp <st>)
   (<st> ^status complete)

-->
   (<r> ^obj1Id <objid>
                <robjid> -)
   (<obj> ^grasped yes
                   no -)
   
   (<out> ^grasp <st> -)
}